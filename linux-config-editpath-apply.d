#!/usr/bin/env dub
/+ dub.sdl:
 dependency "ae" version="==0.0.3569"
 dflags "-i"  # https://github.com/dlang/dub/issues/2638
+/

/**
   Apply the diff between two kernel .config files, as generated by
   linux-config-bisectrepo-gen.
*/

module linux_config_bisectrepo_apply;

import std.algorithm;
import std.exception;
import std.file;
import std.path;
import std.stdio;
import std.string;

import ae.utils.main;
import ae.utils.funopt;

import linux_config_common;

enum FN = ".config";

void linuxConfigEditPathApply(string oldFile, string diff)
{
	bool[string] both; // unused
	string[string] vals;

	loadConfig(oldFile, vals, both);

	foreach (line; diff.readText().splitLines())
	{
		if (!line.length)
			continue;
		auto oline = line;
		scope(failure) stderr.writeln("Error with line: ", oline);
		auto cmd = line[0];
		line = line[1..$];
		auto parts = line.findSplit("=");
		switch (cmd)
		{
			case '-':
				enforce(line in vals, "Attempting to remove nonexisting setting");
				vals.remove(line);
				break;
			case '+':
				enforce(parts[0] !in vals, "Attempting to add already-existing setting");
				vals[parts[0]] = parts[2];
				break;
			case '=':
				enforce(parts[0] in vals, "Attempting to set nonexisting setting");
				vals[parts[0]] = parts[2];
				break;
			default:
				throw new Exception("Unknown command");
		}
	}

	foreach (key; vals.keys.sort())
		writeln(key, "=", vals[key]);
}

mixin main!(funopt!linuxConfigEditPathApply);
